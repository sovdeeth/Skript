test "EvtBreedJUnit" when running JUnit:
	set {_tests::1} to "correct breeder"
	set {_tests::2} to "correct breeding mother"
	set {_tests::3} to "correct breeding father"
	set {_tests::4} to "correct bred child"
	set {_tests::5} to "item is carrot"

	ensure junit test "org.skriptlang.skript.test.tests.syntaxes.events.EvtBreedTest" completes {_tests::*}

on breed of pig:
	set {_test} to "org.skriptlang.skript.test.tests.syntaxes.events.EvtBreedTest"
	junit test is {_test}

	if:
		breeder is a player
		breeder's name is "Efnilite"
	then:
		complete objective "correct breeder" for {_test}

	if:
		breeding mother is a pig
		breeding mother's name is "mother"
	then:
		complete objective "correct breeding mother" for {_test}

	if:
		breeding father is a pig
		breeding father's name is "father"
	then:
		complete objective "correct breeding father" for {_test}

	if:
		bred child is pig
		bred child's name is "child"
	then:
		complete objective "correct bred child" for {_test}

	if event-item is a carrot:
		complete objective "item is carrot" for {_test}
