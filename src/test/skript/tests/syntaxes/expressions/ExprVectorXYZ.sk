test "vector xyz":
	assert the x component of vector(0, 0, 0) is 0 with "x = 0 vector component failed"
	assert the x component of vector(1, 0, 0) is 1 with "x = 1 vector component failed"
	assert the y component of vector(1, 1, 0) is 1 with "y = 1 vector component failed"
	assert the z component of vector(1, 1, 1) is 1 with "z = 1 vector component failed"
	loop 60 times:
		set {_x} to a random number between -100 and 100
		set {_y} to a random number between -100 and 100
		set {_z} to a random number between -100 and 100
		set {_vector} to vector({_x}, {_y}, {_z})
		assert the x component of {_vector} is {_x} with "randomly-created vector x equality failed (expected %{_x}%, got %x component of {_vector}%)"
		assert the y component of {_vector} is {_y} with "randomly-created vector y equality failed (expected %{_y}%, got %y component of {_vector}%)"
		assert the z component of {_vector} is {_z} with "randomly-created vector z equality failed (expected %{_z}%, got %z component of {_vector}%)"
	set {_random} to a random vector
	set {_vector} to a new vector from x component of {_random}, y component of {_random}, z component of {_random}
	assert {_vector} is {_random} with "random vector to created component equality failed (expected %{_random}%, got %{_vector}%)"
