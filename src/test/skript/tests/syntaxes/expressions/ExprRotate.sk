test "rotate around global axis":
	set {_v::1} to quaternion(1, 0, 0, 0)

	set {_v::*} to {_v::*} rotated around y axis by 90 degrees
	assert {_v::1} is quaternion(0.7071067690849304, 0, 0.7071067690849304, 0) with "rotate quaternion around y axis failed"

	set {_v::*} to {_v::*} rotated around x axis by -90 degrees
	assert {_v::1} is quaternion(0.4999999701976776, -0.4999999701976776, 0.4999999701976776, -0.4999999701976776) with "rotate quaternion around x axis failed"

	set {_v::*} to {_v::*} rotated around z axis by -90 degrees
	assert {_v::1} is quaternion(0, 0, 0.7071067094802856, -0.7071067094802856) with "rotate quaternion around z axis failed"

test "rotate around local axis":
	set {_v::1} to quaternion(1, 0, 0, 0)

	set {_v::*} to {_v::*} rotated around local y axis by 90 degrees
	assert {_v::1} is quaternion(0.7071067690849304, 0, 0.7071067690849304, 0) with "rotate quaternion around local y axis failed"

	set {_v::*} to {_v::*} rotated around local x axis by 90 degrees
	assert {_v::1} is quaternion(0.4999999701976776, 0.4999999701976776, 0.4999999701976776, -0.4999999701976776) with "rotate quaternion around local x axis failed"

	set {_v::*} to {_v::*} rotated around local z axis by 90 degrees
	assert {_v::1} is quaternion(0.7071067094802856, 0.7071067094802856, 0, 0) with "rotate quaternion around local z axis failed"

test "vector rotate around vector":
	set {_x} to vector(1, 0, 0)
	set {_y} to vector(0, 1, 0)
	set {_z} to vector(0, 0, 1)

	set {_v} to {_x}
	set {_v} to {_v} rotated around {_y} by -90 degrees
	assert {_v} is {_z} with "rotate around y vector failed"

	set {_v} to {_v} rotated around {_x} by -90 degrees
	assert {_v} is {_y} with "rotate around x vector failed"

	set {_v} to {_v} rotated around {_z} by -90 degrees
	assert {_v} is {_x} with "rotate around z vector failed"

	set {_v} to {_v} rotated around vector(1, 1, 1) by 180 degrees
	assert {_v} is vector(-1/3, 2/3, 2/3) with "rotate around non-orthogonal vector failed"

	set {_v} to vector(2, 0, 0)
	set {_v} to {_v} rotated around vector(0, 10, 0) by 90 degrees
	assert {_v} is vector(0, 0, -2) with "rotate around non-unit vector failed"


test "quaternion rotate by ZYX rotation":
	set {_q} to quaternion(1,0,0,0)
	set {_q} to {_q} rotated by x 90, y 90, z 90
	assert {_q} is quaternion(0.7071067094802856, 0, 0.7071067094802856, 0) with "failed to rotate quaternion by ZYX"
