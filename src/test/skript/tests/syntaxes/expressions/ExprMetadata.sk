test "metadata value":
	spawn armor stand at spawn of "world":
		set metadata "number" of entity to 10
		assert metadata "number" of entity is 10 with "didn't set metadata value"

		add 5 to metadata "number" of entity
		assert metadata "number" of entity is 15 with "didn't add to metadata value"

		remove 3 from metadata "number" of entity
		assert metadata "number" of entity is 12 with "didn't remove from metadata value"

		add "not a number" to metadata "number" of entity
		assert metadata "number" of entity is 12 with "added incompatible type to metadata"

		remove "not a number" from metadata "number" of entity
		assert metadata "number" of entity is 12 with "removed incompatible type from metadata"

		add vector(1, 0, 1) to metadata "vector" of entity
		assert metadata "vector" of entity is vector(1, 0, 1) with "didn't add to unset metadata value"

		delete metadata "number" and "vector" of entity
		assert metadata "number" and "vector" of entity are not set with "didn't delete metadata value"

		delete entity
