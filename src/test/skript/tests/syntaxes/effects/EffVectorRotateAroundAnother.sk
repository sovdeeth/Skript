test "vector rotate around vector":
	set {_x} to vector(1, 0, 0)
	set {_y} to vector(0, 1, 0)
	set {_z} to vector(0, 0, 1)

	set {_v} to {_x}
	rotate {_v} around {_y} by -90 degrees
	assert {_v} is {_z} with "rotate around y vector failed (expected %{_z}%, got %{_v}%)"

	rotate {_v} around {_x} by -90 degrees
	assert {_v} is {_y} with "rotate around x vector failed (expected %{_y}%, got %{_v}%)"

	rotate {_v} around {_z} by -90 degrees
	assert {_v} is {_x} with "rotate around z vector failed (expected %{_x}%, got %{_v}%)"

	rotate {_v} around vector(1, 1, 1) by 180 degrees
	assert {_v} is vector(-1/3, 2/3, 2/3) with "rotate around non-orthogonal vector failed (expected %vector(-1/3, 2/3, 2/3)%, got %{_v}%)"

	set {_v} to vector(2, 0, 0)
	rotate {_v} around vector(0, 10, 0) by 90 degrees
	assert {_v} is vector(0, 0, -2) with "rotate around non-unit vector failed (expected %vector(0, 0, -2)%, got %{_v}%)"
